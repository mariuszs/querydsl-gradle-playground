apply plugin: 'java'
apply plugin: 'groovy'

version = '1.0'

repositories {
    mavenCentral()
}


sourceSets {
    generated {
        java {
            srcDirs = ['build/generated']
        }
    }
}

configurations {
    querydslapt
}


dependencies {
    compile 'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final'
    compile "com.mysema.querydsl:querydsl-jpa:3.6.0"
    compile 'org.codehaus.groovy:groovy-all:2.3.9'

    querydslapt "com.mysema.querydsl:querydsl-apt:3.6.0"

    testCompile group: 'junit', name: 'junit', version: '4.11'

}

task generateQueryDSL(type: GroovyCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

clean {
    delete sourceSets.generated.java.srcDirs
}

idea {
    module {
        sourceDirs += file('build/generated')
    }
}